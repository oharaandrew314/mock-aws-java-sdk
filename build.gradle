plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
    id 'jacoco'
    id 'maven-publish'
}

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
}

ext {
    ver = [
        aws: '1.11.309+',
        awsV2: '2.17.34+'
    ]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-secretsmanager', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-sns', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-cloudformation', version: ver.aws

    compileOnly "software.amazon.awssdk:s3:${ver.awsV2}"
    compileOnly "software.amazon.awssdk:sns:${ver.awsV2}"
    compileOnly "software.amazon.awssdk:cloudformation:${ver.awsV2}"
    compileOnly "software.amazon.awssdk:sqs:${ver.awsV2}"
    compileOnly "software.amazon.awssdk:secretsmanager:${ver.awsV2}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.assertj:assertj-core:3.20.2'
    testImplementation 'io.kotest:kotest-runner-junit5:4.6.2'
}

configurations { // don't want to bundle SDKs in library, but they are needed in tests
    testImplementation.extendsFrom compileOnly
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.kotlin
            artifact sourcesJar
        }
    }
}

test {
    useJUnitPlatform()
}