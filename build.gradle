plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.70'
    id 'jacoco'
    id 'maven-publish'
}

sourceCompatibility = 8
targetCompatibility = 8

repositories {
    mavenCentral()
}

ext {
    ver = [
        aws: '1.11.309+'
    ]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: ver.aws
    compileOnly group: 'com.amazonaws', name: 'aws-java-sdk-secretsmanager', version: ver.aws

    testImplementation group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: ver.aws
    testImplementation group: 'com.amazonaws', name: 'aws-java-sdk-sqs', version: ver.aws
    testImplementation group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: ver.aws
    testImplementation group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: ver.aws
    testImplementation group: 'com.amazonaws', name: 'aws-java-sdk-secretsmanager', version: ver.aws

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.assertj:assertj-core:3.13.2'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.97 // jacoco doesn't play very nice with kotlin
            }
        }
    }
}
check.dependsOn jacocoTestCoverageVerification

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.kotlin
            artifact sourcesJar
        }
    }
}